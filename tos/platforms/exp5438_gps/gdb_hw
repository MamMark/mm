set $_gps_reset_addr  = 0x223
set $_gps_reset_bit   = 0xfd
set $_gps_unreset_bit = 0x02
define gps
set $_p4in   = *(uint8_t *) 0x0221
set $_p4out  = *(uint8_t *) 0x0223
set $_p4dir  = *(uint8_t *) 0x0225
printf "\ngps(ub1): on_off: %d  nRst: %d  CSn: %d  awake: %d\n", \
    ($_p4out & 1), (($_p4out >> 1) & 1), (($_p4out >> 2) & 1), (($_p4in >> 3) & 1)
printf "                  %c        %c       %c         %c\n", \
    ($_p4dir&1)?'O':'I', ($_p4dir&2)?'O':'I', ($_p4dir&4)?'O':'I', ($_p4dir&8)?'O':'I'
set $_ub = 0x0620
printf "B1: IE: %02x   IFG: %02x  IV: %04x\n", \
    (*(uint8_t *)($_ub+0x1c)), (*(uint8_t *)($_ub+0x1d)), (*(uint8_t *)($_ub+0x1e))
printf "  ctl0: %02x  ctl1: %02x  br: %02x%02x  mctl: %02x  stat: %02x  rx: %02x  tx: %02x\n\n",\
    (*(uint8_t *)($_ub+1)), (*(uint8_t *)($_ub+0)),   (*(uint8_t *)($_ub+7)),   (*(uint8_t *)($_ub+6)),\
    (*(uint8_t *)($_ub+8)), (*(uint8_t *)($_ub+0xa)), (*(uint8_t *)($_ub+0xc)), (*(uint8_t *)($_ub+0xe))
printf "gpsc_state: "
output gpsc_state
printf "    gpsm_state: "
output gpsm_state
printf "    collect_state: "
output GPSMsgP__collect_state
printf "\n"
end
document gps
display gps state
end

define rg
set (*(uint8_t*)$_gps_reset_addr)=(*(uint8_t*)$_gps_reset_addr) & $_gps_reset_bit
set (*(uint8_t*)$_gps_reset_addr)=(*(uint8_t*)$_gps_reset_addr) | $_gps_unreset_bit
end
document rg
reset gps h/w
end

define radio
set $_p1in   = *(uint8_t *) 0x0200
set $_p1dir  = *(uint8_t *) 0x0204
set $_p10out = *(uint8_t *) 0x0283
set $_p10dir = *(uint8_t *) 0x0285
printf "\nradio(ua3): cts: %d  irqN: %d  sdn: %d  csN: %d\n", \
    (($_p1in >> 2) & 1), (($_p1in >> 4) & 1), (($_p10out >> 6) & 1), (($_p10out >> 7) & 1)
printf "                 %c        %c       %c       %c\n", \
    ($_p1dir&0x4)?'O':'I', ($_p1dir&0x10)?'O':'I', ($_p10dir&0x40)?'O':'I', ($_p10dir&0x80)?'O':'I'
set $_ub = 0x0680
printf "A3: IE: %02x   IFG: %02x  IV: %04x\n", \
    (*(uint8_t *)($_ub+0x1c)), (*(uint8_t *)($_ub+0x1d)), (*(uint8_t *)($_ub+0x1e))
printf "  ctl0: %02x  ctl1: %02x  br: %02x%02x  mctl: %02x  stat: %02x  rx: %02x  tx: %02x\n\n",\
    (*(uint8_t *)($_ub+1)), (*(uint8_t *)($_ub+0)),   (*(uint8_t *)($_ub+7)),   (*(uint8_t *)($_ub+6)),\
    (*(uint8_t *)($_ub+8)), (*(uint8_t *)($_ub+0xa)), (*(uint8_t *)($_ub+0xc)), (*(uint8_t *)($_ub+0xe))
end
document radio
display radio state
end

define cts
set $_p1in   = *(uint8_t *) 0x0200
set $_p1dir  = *(uint8_t *) 0x0204
set $_p10out = *(uint8_t *) 0x0283
set $_p10dir = *(uint8_t *) 0x0285
printf "cts: %d  irqN: %d  sdn: %d  csN: %d\n", \
    (($_p1in >> 2) & 1), (($_p1in >> 4) & 1), (($_p10out >> 6) & 1), (($_p10out >> 7) & 1)
end
document cts
current status of various Si446x radio pins
end

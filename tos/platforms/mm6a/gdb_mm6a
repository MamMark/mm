define radio
printf "\nradio(ub2): cts: %d  irqN: %d  sdn: %d  csN: %d   siok\n", \
    (($p4->IN >> 1) & 1),  (($p6->IN >> 1) & 1), \
    (($p3->OUT >> 3) & 1), (($p3->OUT >> 4) & 1)
printf "                 %c        %c       %c       %c    %c%c%c\n", \
    ($p4->DIR & (1<<1))?'O':'I', ($p6->DIR & (1<<1))?'O':'I', \
    ($p3->DIR & (1<<3))?'O':'I', ($p3->DIR & (1<<4))?'O':'I', \
    ($p3->SEL0 & (1 << 5)) ? 'M' : 'p', ($p3->SEL0 & (1 << 7)) ? 'M' : 'p', \
    ($p3->SEL0 & (1 << 6)) ? 'M' : 'p'
__p_usci $ucb2 2 1 0d22
end
document radio
display radio state
end

define cts
printf "cts: %d  irqN: %d  sdn: %d  csN: %d\n",  \
    (($p4->IN  >> 1) & 1), (($p6->IN  >> 1) & 1), \
    (($p3->OUT >> 3) & 1), (($p3->OUT >> 4) & 1)
end
document cts
current status of various Si446x radio pins
end

define sd0
printf "\nuSD0(ua2): pwr: %d  csN: %d    siok\n", \
    (($p7->OUT >> 6) & 1), (($p3->OUT >> 1) & 1)
printf "                %c       %c     %c%c%c\n", \
    ($p7->DIR & (1 << 6))  ? 'O' : 'I', ($p3->DIR & (1 << 1)) ?'O':'I', \
    ($p3->SEL0 & (1 << 0)) ? 'M' : 'p', ($p2->SEL0 & (1 << 4)) ? 'M' : 'p', \
    ($p7->SEL0 & (1 << 7)) ? 'M' : 'p'
__p_usci $uca2 2 0 0d18
output SDspP__0__sdc
printf "\n"
end
document sd0
display sd0 state
end

define sd1
printf "\nuSD1(ua1): pwr: %d  csN: %d    siok\n", \
    1, (($p1->OUT >> 3) & 1)
printf "                %c       %c     %c%c%c\n", \
    'O', ($p1->DIR & (1 << 3)) ?'O':'I', \
    ($p2->SEL0 & (1 << 3)) ? 'M' : 'p', ($p3->SEL0 & (1 << 2)) ? 'M' : 'p', \
    ($p2->SEL0 & (1 << 0)) ? 'M' : 'p'
__p_usci $uca1 1 0 0d17
output SDspP__1__sdc
printf "\n"
end
document sd1
display sd1 state
end

define gps
printf "\ngps(ua0): on_off: %d  nRst: %d  awake: %d    grt\n", \
    (($p5->OUT >> 5) & 1), (($pj->OUT >> 2) & 1), (($p6->IN >> 2) & 1)
printf "                  %c        %c         %c     %c%c\n", \
    ($p5->DIR & (1<<5))?'O':'I', ($pj->DIR & (1<<2))?'O':'I', \
    ($p6->DIR & (1<<2))?'O':'I', \
    ($p7->SEL0 & (1 << 3)) ? 'M' : 'p', ($p7->SEL0 & (1 << 2)) ? 'M' : 'p'
__p_usci $uca0 0 0 0d16
end
document gps
display gps state
end
document gps
display gps state
end

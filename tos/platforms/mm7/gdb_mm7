
printf "\n***\n*** MM7 gdb\n***\n\n"
source .gdb/.gdb_hwMm.py

define hw_pwr
set $_pwr_tmp_en      = (($p1->OUT >> 0) & 1)
set $_pwr_tmp_dir     = ($p1->DIR  & (1<<0))?' ':'i'
set $_batt_chrg       = (($p1->OUT >> 2) & 1)
set $_batt_chrg_dir   = ($p1->DIR  & (1<<2))?' ':'i'
set $_scl_in          = (($p6->IN  >> 7) & 1)
set $_scl_dir         = ($p6->DIR  & (1<<7))?' ':'i'
set $_scl_sel         = ($p6->SEL0 & (1<<7))?'m':'p'
set $_sda_in          = (($p6->IN  >> 6) & 1)
set $_sda_dir         = ($p6->DIR  & (1<<6))?' ':'i'
set $_sda_sel         = ($p6->SEL0 & (1<<6))?'m':'p'
set $_pwr_sd0_en      = (($p8->OUT >> 0) & 1)
set $_pwr_sd0_dir     = ($p8->DIR  & (1<<0))?' ':'i'
set $_gps_vbckup      = (($pj->OUT >> 2) & 1)
set $_gps_vbckup_dir  = ($pj->DIR  & (1<<2))?' ':'i'
set $_gps_pwr         = (($pj->OUT >> 4) & 1)
set $_gps_pwr_dir     = ($pj->DIR  & (1<<4))?' ':'i'

printf "tmp_pwr: %d%c  batt_chrg: %d%c  sd_pwr: %d%c  gps_vbckup: %d%c  gps_pwr: %d%c  scl: %d%c%c  sda: %d%c%c\n",          \
    $_pwr_tmp_en, $_pwr_tmp_dir, $_batt_chrg, $_batt_chrg_dir, $_pwr_sd0_en, $_pwr_sd0_dir,                                  \
    $_gps_vbckup, $_gps_vbckup_dir, $_gps_pwr, $_gps_pwr_dir, $_scl_in, $_scl_dir, $_scl_sel, $_sda_in, $_sda_dir, $_sda_sel
end

define hw_radio
set $_radio_csn      = ($p3->OUT >> 3) & 1
set $_radio_csn_dir  = ($p3->DIR  & (1<<3))?'O':'I'
set $_radio_irqn     = ($p3->IN  >> 7) & 1
set $_radio_irqn_dir = ($p3->DIR  & (1<<7))?'O':'I'
set $_radio_sdn      = ($p4->OUT >> 3) & 1
set $_radio_sdn_dir  = ($p4->DIR  & (1<<3))?'O':'I'
set $_radio_cts      = ($p4->IN  >> 4) & 1
set $_radio_cts_dir  = ($p4->DIR  & (1<<4))?'O':'I'
set $_radio_gp0      = ($p4->IN  >> 5) & 1
set $_radio_gp0_dir  = ($p4->DIR  & (1<<5))?'O':'I'

set $_radio_simo_sel = ($p3->SEL0 & (1<<4))?'M':'p'
set $_radio_somi_sel = ($p3->SEL0 & (1<<5))?'M':'p'
set $_radio_sclk_sel = ($p3->SEL0 & (1<<6))?'M':'p'

printf "\nradio(ub2): cts: %d  irqN: %d  sdn: %d  csN: %d   siok\n",     \
    $_radio_cts, $_radio_irqn, $_radio_sdn, $_radio_csn
printf "                 %c        %c       %c       %c    %c%c%c\n",    \
    $_radio_cts_dir, $_radio_irqn_dir, $_radio_sdn_dir, $_radio_csn_dir, \
    $_radio_simo_sel, $_radio_somi_sel, $_radio_sclk_sel
__p_usci $ucb2 2 1 0d22
end
document hw_radio
display radio hardware state
end

define cts
set $_radio_csn      = ($p3->OUT >> 3) & 1
set $_radio_irqn     = ($p3->IN  >> 7) & 1
set $_radio_sdn      = ($p4->OUT >> 3) & 1
set $_radio_cts      = ($p4->IN  >> 4) & 1
set $_radio_gp0      = ($p4->IN  >> 5) & 1
printf "cts: %d  irqN: %d  sdn: %d  csN: %d\n",  \
    $_radio_cts, $_radio_irqn, $_radio_sdn, $_radio_csn
end
document cts
current status of various Si446x radio pins
end

define sd0

set $_pwr_sd0_en   = ($p8->OUT >> 0) & 1
set $_pwr_sd0_dir  = ($p8->DIR  & (1<<0))?'O':'I'
set $_sd0_csn      = ($p8->OUT >> 1) & 1
set $_sd0_csn_dir  = ($p8->DIR  & (1<<1))?'O':'I'

set $_sd0_somi_sel = ($p3->SEL0 & (1<<0))?'M':'p'
set $_sd0_sclk_sel = ($p3->SEL0 & (1<<1))?'M':'p'
set $_sd0_simo_sel = ($p3->SEL0 & (1<<2))?'M':'p'

printf "\nuSD0(ua2): pwr: %d  csN: %d    siok\n", \
    $_pwr_sd0_en, $_sd0_csn
printf "                %c       %c     %c%c%c\n", \
    $_pwr_sd0_dir, $_sd0_csn_dir, \
    $_sd0_simo_sel, $_sd0_somi_sel, $_sd0_sclk_sel
__p_usci $uca2 2 0 0d18
output SDspP__0__sdc
printf "\n"
printf "Delta times:\n"
printf "  last_full_reset_time_us:  0d%d\n", \
       SDspP__0__last_full_reset_time_us
printf "  last_reset_delta_ms: 0d%d/%d  _us: 0d%d/%d\n", \
          SDspP__0__last_reset_delta_ms, SDspP__0__max_reset_time_ms, \
          SDspP__0__last_reset_delta_us, SDspP__0__max_reset_time_us
printf "  last_read_delta_ms: 0d%d/%d  _us: 0d%d/%d\n", \
          SDspP__0__last_read_delta_ms, SDspP__0__max_read_time_ms,   \
          SDspP__0__last_read_delta_us, SDspP__0__max_read_time_us
printf "  last_write_delta_ms: 0d%d/%d  _us: 0d%d/%d\n", \
          SDspP__0__last_write_delta_ms, SDspP__0__max_write_time_ms, \
          SDspP__0__last_write_delta_us, SDspP__0__max_write_time_us
printf "  last_erase_delta_ms: 0d%d/%d  _us: 0d%d/%d\n", \
          SDspP__0__last_erase_delta_ms, SDspP__0__max_erase_time_ms, \
          SDspP__0__last_erase_delta_us, SDspP__0__max_erase_time_us
printf "DMA stats:\n"
output/d SD0HardwareP__dma_stats
printf "\n"
end
document sd0
display sd0 state
end


define hw_gps

set $_gps_csn_out = (($p5->OUT  >> 5) & 1)
set $_gps_csn_dir = (($p5->DIR  & (1 << 5)) ? 'O' : 'I')
set $_gps_csn_mod = (($p5->SEL0 & (1 << 5)) ? 'M' : 'p')
set $_gps_csn_ren = (($p5->REN  & (1 << 5)) ? 'r' : '-')

set $_gps_txrdy_in  = (($p5->IN   >> 7) & 1)
set $_gps_txrdy_dir = (($p5->DIR  & (1 << 7)) ? 'O' : 'I')
set $_gps_txrdy_mod = (($p5->SEL0 & (1 << 7)) ? 'M' : 'p')
set $_gps_txrdy_ren = (($p5->REN  & (1 << 7)) ? 'r' : '-')

# gps_tm
set $_gps_tm_in  = (($p7->IN   >> 1) & 1)
set $_gps_tm_dir = (($p7->DIR  & (1 << 1)) ? 'O' : 'I')
set $_gps_tm_mod = (($p7->SEL0 & (1 << 1)) ? 'M' : 'p')
set $_gps_tm_ren = (($p7->REN  & (1 << 1)) ? 'r' : '-')

if ($p7->REN & (1 << 0))
    set $_gps_sclk_val = 'r'
else
    if ($p7->SEL0 & (1 << 0))
        set $_gps_sclk_val = 'm'
    else
        if ($p7->DIR & (1 << 0))
            set $_gps_sclk_val = 'O'
        else
            set $_gps_sclk_val = 'I'
        end
    end
end

if ($p7->REN & (1 << 2))
    set $_gps_somi_val = 'r'
else
    if ($p7->SEL0 & (1 << 2))
        set $_gps_somi_val = 'm'
    else
        if ($p7->DIR & (1 << 2))
            set $_gps_somi_val = 'O'
        else
            set $_gps_somi_val = 'I'
        end
    end
end

if ($p7->REN & (1 << 3))
    set $_gps_simo_val = 'r'
else
    if ($p7->SEL0 & (1 << 3))
        set $_gps_simo_val = 'm'
    else
        if ($p7->DIR & (1 << 3))
            set $_gps_simo_val = 'O'
        else
            set $_gps_simo_val = 'I'
        end
    end
end

printf "\ngps(ua1): csn: %d    tm: %d    txrdy: %d    siok\n",        \
                    $_gps_csn_out,  $_gps_tm_in,   $_gps_txrdy_in
printf "              %c%c%c      %c%c%c         %c%c%c    %c%c%c\n", \
       $_gps_csn_dir, $_gps_csn_mod, $_gps_csn_ren,                   \
       $_gps_tm_dir, $_gps_tm_mod, $_gps_tm_ren,                      \
       $_gps_txrdy_dir, $_gps_txrdy_mod, $_gps_txrdy_ren,             \
       $_gps_simo_val,  $_gps_somi_val, $_gps_sclk_val

printf "\n"
__p_usci $uca1 1 0 0d17
end
document hw_gps
display gps hardware state
end
